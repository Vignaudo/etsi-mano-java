plugins {
	id 'war'
	id 'antlr'
	id 'eclipse-wtp'
	id 'jacoco'
	id "com.github.ben-manes.versions" version  '0.33.0'
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'org.sonarqube' version '2.8'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'net.researchgate.release' version '2.8.1'
	id 'com.gorylenko.gradle-git-properties' version '2.2.3'
	id "org.liquibase.gradle" version "2.0.4"
}
apply plugin: 'jacoco'
apply plugin: 'liquibase'

configurations.all {
	exclude group: 'log4j', module: 'log4j'
	exclude group: 'org.hibernate.validator', module: 'hibernate-validator'
	exclude group: 'org.hibernate', module: 'ejb3-persistence'
	exclude group: 'quartz', module: 'quartz'
	exclude group: 'org.jboss.logging', module: 'jboss-logging-spi'
	exclude group: 'javassist', module: 'javassist'
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}
/*
java {
	withJavadocJar()
	withSourcesJar()
}
*/
test {
	useJUnitPlatform() {
		excludeTags 'Remote'
	}
}

sonarqube {
	properties {
		property "sonar.projectName", "etsi-mano"
		property "sonar.host.url", "http://localhost:9011/"
		property "sonar.scm.provider", "git"
	}
}

gitProperties {
	gitPropertiesName = 'git.properties'
	dateFormat = "yyyy-MM-dd'T'HH:mmZ"
	dateFormatTimeZone = 'UTC'
}

group = 'com.ubiqube'
sourceCompatibility = JavaVersion.VERSION_11
project.compileJava.dependsOn(generateGitProperties)

ext {	
	//mainUrl = 'jdbc:postgresql://10.31.1.28/mano'
	mainUrl = 'jdbc:postgresql://192.168.1.14/mano'
	mainUsername = 'mano'
	mainPassword = 'mano'
}

liquibase {
	activities {
		main {
			changeLogFile 'src/main/resources/db/changelog/changes/v0001.groovy'
			url project.ext.mainUrl
			username project.ext.mainUsername
			password project.ext.mainPassword
			referenceUrl "hibernate:spring:com.ubiqube.etsi.mano.dao?dialect=org.hibernate.dialect.PostgreSQL93Dialect&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy"
		}
	}
}

dependencies {
	compileOnly("org.springframework.boot:spring-boot-devtools")

	liquibaseRuntime 'org.liquibase:liquibase-core:3.10.0'
	liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.1.2'
	liquibaseRuntime 'org.postgresql:postgresql:42.2.16'
	liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:3.10.0'
	liquibaseRuntime 'org.springframework.boot:spring-boot-starter-data-jpa'
	liquibaseRuntime sourceSets.main.output

	testImplementation('org.junit.jupiter:junit-jupiter:5.7.0')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
	testCompile group: 'commons-jxpath', name: 'commons-jxpath', version: '1.3'
	testCompile 'com.h2database:h2'
	testCompile 'org.jeasy:easy-random-core:4.2.0'
	testCompile group: 'uk.co.jemos.podam', name: 'podam', version: '7.2.5.RELEASE'

	implementation 'com.google.code.findbugs:jsr305:2.0.1'
	implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.12'
	//compile group: "com.github.fge", name: "json-patch", version: '1.9'

	compile group: 'com.github.joschi.jackson', name: 'jackson-datatype-threetenbp', version: '2.11.2'

	compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.2'

	compile("org.springframework.boot:spring-boot-starter-security")
	compile("org.springframework.boot:spring-boot-starter-web") {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.boot:spring-boot-starter-quartz'
	// https://github.com/vromero/activemq-artemis-docker
	implementation 'org.springframework.boot:spring-boot-starter-artemis'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.liquibase:liquibase-core'
	runtimeOnly 'org.postgresql:postgresql:42.2.16'

	compile 'org.slf4j:log4j-over-slf4j:1.7.30'

	implementation 'org.jgrapht:jgrapht-core:1.5.0'
	implementation 'org.jgrapht:jgrapht-io:1.5.0'
	implementation 'guru.nidi:graphviz-java:0.17.0'
	//
	compile group: 'com.github.dexecutor', name: 'dexecutor-core', version: '2.1.1'
	// https://github.com/akihyro/orika-spring-boot-starter
	implementation 'net.rakugakibox.spring.boot:orika-spring-boot-starter:1.9.0'

	implementation 'org.hibernate:hibernate-search:5.11.5.Final'

	compile 'org.eclipse.jdt:org.eclipse.jdt.annotation:2.2.600'
	
	compile group: 'com.github.openstack4j.core', name: 'openstack4j-core', version: '3.9-SNAPSHOT'
	implementation 'com.github.openstack4j.core.connectors:openstack4j-httpclient:3.9-SNAPSHOT'
	
	
	// Tosca.
	compile 'com.ubiqube.mano:tosca-vnfd-class:0.1.0-SNAPSHOT'
	compile 'com.ubiqube.mano:tosca-nsd-class:0.1.0-SNAPSHOT'
	compile 'com.ubiqube.mano:mano-pkg-tosca:0.0.1-SNAPSHOT'
	// ETSI-MANO 2.6.1 APIs.
	compile group: 'com.ubiqube.mano', name: 'etsi-nfvo-261', version: '1.0-SNAPSHOT'
	compile group: 'com.ubiqube.mano', name: 'etsi-vnfm-261', version: '1.0-SNAPSHOT'

	compile group: 'de.codecentric', name: 'spring-boot-admin-starter-client', version: '2.3.0'

}
bootWar {
	manifest {
		attributes(
		 'Dependencies': 'jdk.unsupported'
		 )
	 }
}
springBoot {
	mainClassName = 'com.ubiqube.etsi.mano.Application'
}
generateGrammarSource {
	maxHeapSize = "64m"
	arguments += ["-visitor", "-long-messages"]
}

task copyDependencies(type: Copy) {
   from configurations.compile
   into 'dependencies'
}


jacocoTestReport {
	dependsOn test
	reports {
		xml.enabled true
		csv.enabled false
		html.destination file("${buildDir}/jacocoHtml")
	}
}


publishing {
	publications {
		maven(MavenPublication) {
			groupId = 'com.ubiqube.mano'
			from components.java
		}
	}
}
