plugins {
	id 'war'
	id 'antlr'
	id 'eclipse-wtp'
	id 'jacoco'
	id "com.github.ben-manes.versions" version  '0.27.0'
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'org.sonarqube' version '2.8'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'net.researchgate.release' version '2.8.1'
	id 'com.gorylenko.gradle-git-properties' version '2.2.0'
}

configurations.all {
	exclude group: 'log4j', module: 'log4j'
	exclude group: 'org.hibernate.validator', module: 'hibernate-validator'
	exclude group: 'org.hibernate', module: 'ejb3-persistence'
	exclude group: 'quartz', module: 'quartz'
	exclude group: 'org.jboss.logging', module: 'jboss-logging-spi'
	exclude group: 'javassist', module: 'javassist'
}

test {
	useJUnitPlatform() {
		excludeTags 'Remote'
	}
}

sonarqube {
	properties {
		property "sonar.projectName", "etsi-mano"
		property "sonar.host.url", "http://localhost:9011/"
	}
}

gitProperties {
	gitPropertiesName = 'git.properties'
	dateFormat = "yyyy-MM-dd'T'HH:mmZ"
	dateFormatTimeZone = 'UTC'
}

group = 'com.ubiqube'
sourceCompatibility = 1.8
project.compileJava.dependsOn(generateGitProperties)

repositories {
	mavenCentral()
	flatDir {
		dirs 'sharedLib/ubiqube'
	}
}
ext {
	// Use -Penv=wildfly for WildFly other wise jboss is activated.
	wildflyDeps = {
		runtimeOnly group: 'org.wildfly', name: 'wildfly-ejb-client-bom', version: '10.0.0.Final'
	}
	jbossDeps = {
		compile(group: 'org.jboss.jbossas', name: 'jboss-as-client', version: '5.1.0.GA') {
			exclude group: 'javax.security', module: 'jaas'
			exclude group: 'javax.security', module: 'jacc'
			exclude group: 'org.slf4j', module: 'slf4j-api'
			exclude group: 'org.jboss.slf4j', module: 'slf4j-jboss-logging'
			exclude group: 'org.hibernate', module: 'hibernate-annotations'
		}
		compile group: 'org.jboss.naming', name: 'jnp-client', version: '5.0.5.Final'
		compile group: 'concurrent', name: 'concurrent', version: '1.3.4'
		compile group: 'commons-io', name: 'commons-io', version: '2.6'
	}
	env = findProperty('env') ?: 'wildfly'
}
if(env == 'wildfly') {
	dependencies project.wildflyDeps
	println 'Building: for wildfly'
} else {
	dependencies project.jbossDeps
	println 'Building: for jboss'
}
dependencies {
	testImplementation('org.junit.jupiter:junit-jupiter:5.6.0')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
	testCompile group: 'commons-jxpath', name: 'commons-jxpath', version: '1.3'

	implementation 'com.google.code.findbugs:jsr305:2.0.1'
	implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.11'
	// Json lib is an EJB dependency.
	implementation group: 'net.sf.json-lib', name: 'json-lib', version: '2.4', classifier: 'jdk15'
	//compile group: "com.github.fge", name: "json-patch", version: '1.9'

	compile group: 'io.swagger', name: 'swagger-annotations', version: '1.5.24'
	compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '3.0.0-SNAPSHOT'
	compile group: 'io.springfox', name: 'springfox-swagger2', version: '3.0.0-SNAPSHOT'
	implementation 'io.springfox:springfox-spring-webmvc:3.0.0-SNAPSHOT'

	compile group: 'com.github.joschi.jackson', name: 'jackson-datatype-threetenbp', version: '2.10.0'

	compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.2'

	compile name: 'ubi-api-client'
	compile name: 'ubi-jcommons'

	compile("org.springframework.boot:spring-boot-starter-security")
	compile("org.springframework.boot:spring-boot-starter-web") {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.boot:spring-boot-starter-quartz'
	// https://github.com/vromero/activemq-artemis-docker
	implementation 'org.springframework.boot:spring-boot-starter-artemis'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	// implementation 'org.liquibase:liquibase-core'
	runtimeOnly 'org.postgresql:postgresql:42.2.9'

	compile 'org.slf4j:log4j-over-slf4j:1.7.26'
	compile 'org.webjars:swagger-ui:3.24.3'

	implementation 'org.jgrapht:jgrapht-core:1.3.1'
	// https://github.com/akihyro/orika-spring-boot-starter
	implementation 'net.rakugakibox.spring.boot:orika-spring-boot-starter:1.9.0'

	implementation 'org.hibernate:hibernate-search:5.11.4.Final'

	compile 'org.eclipse.jdt:org.eclipse.jdt.annotation:2.2.400'
}

springBoot {
	mainClassName = 'com.ubiqube.etsi.mano.Application'
}
generateGrammarSource {
	maxHeapSize = "64m"
	arguments += ["-visitor", "-long-messages"]
}

task copyDependencies(type: Copy) {
   from configurations.compile
   into 'dependencies'
}

